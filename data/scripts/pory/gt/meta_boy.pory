
// adds an NPC that gives a Johto Starter in Dewford's Pokémon Center

const GIVEMON_HELPER_SPECIES = VAR_TEMP_9
const GIVEMON_HELPER_ARTICLE = VAR_TEMP_8
const META_BOY_ID = 4

script Pory_EventScript_MetaBoy_GiveJohtoStarter {
  lock
  faceplayer
  if (!flag(FLAG_TEMP_1)) {
    if (!flag(FLAG_TEMP_2)) {
      msgbox(format("Hmm… My parents got me this cool, rare Pokémon from Johto and everything, but I don't think I'm cut out to be a Trainer.\pI just want to go home and make mods of classic video games from the 2000s.\pI'd give it to a strong enough Trainer, if I could just find the right one…"))
      setflag(FLAG_TEMP_2)
    } else {
      msgbox(format("I just don't think I'm cut out to be a Trainer.\pI'd give my Pokémon to a strong enough Trainer right now…\p{FONT_SMALL}…I could be laying on the beach right now, modding my video games…"))
    }
    if (flag(FLAG_BADGE02_GET)) {
      playse(SE_PIN)
      applymovement(META_BOY_ID, Common_Movement_ExclamationMark)
      waitmovement(0)
      applymovement(META_BOY_ID, Common_Movement_Delay48)
      waitmovement(0)
      msgbox(format("Wait a second…\pThat confidence…\pThat badge…\pAnd, most of all, that plot armor…\p…YOU could take care of my Pokémon!\pPlease, oh please, would you?\nPleasy please?"), MSGBOX_YESNO)
      if (var(VAR_RESULT)) {
        msgbox(format("Yay! you're the best!"))
        switch (var(VAR_STARTER_MON)) {
          case 0:
            givemon(SPECIES_CROCONAW, 25)
            setvar(GIVEMON_HELPER_SPECIES, SPECIES_CROCONAW)
          case 1:
            givemon(SPECIES_BAYLEEF, 25)
            setvar(GIVEMON_HELPER_SPECIES, SPECIES_BAYLEEF)
          case 2:
            givemon(SPECIES_QUILAVA, 25)
            setvar(GIVEMON_HELPER_SPECIES, SPECIES_QUILAVA)
        }
        setvar(VAR_TEMP_2, 25)
        bufferspeciesname(STR_VAR_1, VAR_TEMP_1)
        call(Pory_EventScript_GiveMonHelper)
        setflag(FLAG_RECEIVED_JOHTO_STARTER)
        clearflag(FLAG_META_BOY_ON_THE_BEACH)
        setflag(FLAG_TEMP_1)
        msgbox(format("Now I can finally spend the summer at my family's beach house in peace!"))
      } else {
        msgbox(format("But… It's a free Pokémon! And you're the protagonist!"))
      }
    }
  } else {
    msgbox(format("Now I can finally spend the summer at my family's beach house in peace!"))
  }
  release
  end
}

// they also give a Linking Cord on Route 109

script Pory_EventScript_MetaBoy_WhatsUp {
  lock
  faceplayer
  if (!flag(FLAG_RECEIVED_LINKING_CORD)) {
  msgbox(format("Hey, what's up? Remember me? Thanks again for taking care of my Pokémon for me.\p… … … … … … … …\n… … … … … … … …\pWhat's that? How did I get here so fast?\pWell, we can't ALL be the protagonist in this story, can we? You get plot armor and a sense of fulfillment, I get to warp instantaneously to this beach.\p… … … … … … … …\n… … … … … … … …\pOk fine, do you want to check out something REALLY meta?\pTake this, then!"))
  // I mean, technically speaking, I'm actually a completely different set of code and graphical assets than who you talked to in Dewford. You're the one personifying me and creating any sense of continuity here. That's on you."
  giveitem(ITEM_LINKING_CORD)
  goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
  setflag(FLAG_RECEIVED_LINKING_CORD)
  msgbox(format("Seriously though, in the newer games Game Freak has actually coded this item into the game.\pI'll let you figure out what it does on your own time."))
  } else {
    msgbox(format("Hey… You don't happen to know any good mojito recipes, do you?"))
  }
  release
  end
}
