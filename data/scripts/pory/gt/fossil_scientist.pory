// the Fossil Scientist now also restores gen 1 fossils

const GIVEMON_HELPER_ARTICLE = VAR_TEMP_9
const ITEM_TO_CHECK = VAR_TEMP_0

script Pory_EventScript_FossilScientist {
  lock
  faceplayer
  if (!flag(FLAG_TEMP_1)) {
    message(format("I've been trying to develop a device that resurrects Pokémon from fossils…\pAnd, it's working!\pDo you happen to have any? I can bring them back to life with my regenerator!"))
    waitmessage
  } else {
    message(format("Do you have another fossil for me?"))
    waitmessage
  }
  checkitem(ITEM_HELIX_FOSSIL)
  if (var(VAR_RESULT)) {
    goto(Pory_EventScript_FossilScientist_ReviveFossil)
  }
  checkitem(ITEM_DOME_FOSSIL)
  if (var(VAR_RESULT)) {
    goto(Pory_EventScript_FossilScientist_ReviveFossil)
  }
  checkitem(ITEM_OLD_AMBER)
  if (var(VAR_RESULT)) {
    goto(Pory_EventScript_FossilScientist_ReviveFossil)
  }
  checkitem(ITEM_ROOT_FOSSIL)
  if (var(VAR_RESULT)) {
    goto(Pory_EventScript_FossilScientist_ReviveFossil)
  }
  checkitem(ITEM_CLAW_FOSSIL)
  if (var(VAR_RESULT)) {
    goto(Pory_EventScript_FossilScientist_ReviveFossil)
  }
  release
  end
}

script Pory_EventScript_FossilScientist_ReviveFossil {
  setvar(ITEM_TO_CHECK, ITEM_HELIX_FOSSIL)
  call(Pory_EventScript_PushItemIfOwned)
  setvar(ITEM_TO_CHECK, ITEM_DOME_FOSSIL)
  call(Pory_EventScript_PushItemIfOwned)
  setvar(ITEM_TO_CHECK, ITEM_OLD_AMBER)
  call(Pory_EventScript_PushItemIfOwned)
  setvar(ITEM_TO_CHECK, ITEM_ROOT_FOSSIL)
  call(Pory_EventScript_PushItemIfOwned)
  setvar(ITEM_TO_CHECK, ITEM_CLAW_FOSSIL)
  call(Pory_EventScript_PushItemIfOwned)
  dynmultistack(0, 0, FALSE, 6, FALSE, 0, DYN_MULTICHOICE_CB_SHOW_ITEM)
  switch (var(VAR_RESULT)) {
    case ITEM_HELIX_FOSSIL:
      setvar(VAR_WHICH_FOSSIL_REVIVED, ITEM_HELIX_FOSSIL)
      bufferitemname(STR_VAR_1, ITEM_HELIX_FOSSIL)
      msgbox(format("Praise Helix! Let's do it right now.\p{PLAYER} handed the {STR_VAR_1} to the Devon researcher."))
      removeitem(ITEM_HELIX_FOSSIL)
    case ITEM_DOME_FOSSIL:
      setvar(VAR_WHICH_FOSSIL_REVIVED, ITEM_DOME_FOSSIL)
      bufferitemname(STR_VAR_1, ITEM_DOME_FOSSIL)
      msgbox(format("Excellent! Let's do it right now.\p{PLAYER} handed the {STR_VAR_1} to the Devon researcher."))
      removeitem(ITEM_DOME_FOSSIL)
    case ITEM_OLD_AMBER:
      setvar(VAR_WHICH_FOSSIL_REVIVED, ITEM_OLD_AMBER)
      bufferitemname(STR_VAR_1, ITEM_OLD_AMBER)
      msgbox(format("Excellent, let's do it!\p{PLAYER} handed the {STR_VAR_1} to the Devon researcher."))
      removeitem(ITEM_OLD_AMBER)
    case ITEM_ROOT_FOSSIL:
      setvar(VAR_WHICH_FOSSIL_REVIVED, ITEM_ROOT_FOSSIL)
      bufferitemname(STR_VAR_1, ITEM_ROOT_FOSSIL)
      msgbox(format("Excellent! Let's do it right away.\p{PLAYER} handed the {STR_VAR_1} to the Devon researcher."))
      removeitem(ITEM_ROOT_FOSSIL)
    case ITEM_CLAW_FOSSIL:
      setvar(VAR_WHICH_FOSSIL_REVIVED, ITEM_CLAW_FOSSIL)
      bufferitemname(STR_VAR_1, ITEM_CLAW_FOSSIL)
      msgbox(format("Excellent! Let's do it right away.\p{PLAYER} handed the {STR_VAR_1} to the Devon researcher."))
      removeitem(ITEM_CLAW_FOSSIL)
    case MULTI_B_PRESSED:
      msgbox(format("Oh, is that so?\pOur technological expertise here at Devon is outstanding, I tell you."))
      release
      end
  }
  msgbox(format("… … … … … … … …\n… … … … … … … …\p… … … … … … … …\n… … … … … … … …\pThanks for waiting! Your fossil came back to life!"))
  switch (var(VAR_WHICH_FOSSIL_REVIVED)) {
    case ITEM_HELIX_FOSSIL:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_OMANYTE)
      setvar(GIVEMON_HELPER_ARTICLE, 2)
      givemon(SPECIES_OMANYTE, 25)
    case ITEM_DOME_FOSSIL:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_KABUTO)
      setvar(GIVEMON_HELPER_ARTICLE, 1)
      givemon(SPECIES_KABUTO, 25)
    case ITEM_OLD_AMBER:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_AERODACTYL)
      setvar(GIVEMON_HELPER_ARTICLE, 2)
      givemon(SPECIES_AERODACTYL, 25)
    case ITEM_ROOT_FOSSIL:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_LILEEP)
      setvar(GIVEMON_HELPER_ARTICLE, 1)
      givemon(SPECIES_LILEEP, 25)
    case ITEM_CLAW_FOSSIL:
      setvar(VAR_TEMP_TRANSFERRED_SPECIES, SPECIES_ANORITH)
      setvar(GIVEMON_HELPER_ARTICLE, 2)
      givemon(SPECIES_ANORITH, 25)
  }
  call(Pory_EventScript_GiveMonHelper)
  setflag(FLAG_RECEIVED_REVIVED_FOSSIL_MON)
  setflag(FLAG_TEMP_1)
  msgbox(format("Come back with more fossils!"))
  release
  end
}
