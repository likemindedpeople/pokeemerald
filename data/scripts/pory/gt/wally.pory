// removes mention of Ralts as Wally's Pokémon

text MauvilleCity_Text_WallyWeCanBeatAnyone_Pory {
  format("Wally: I'm not pushing it.\pIf I combine forces with my Pokémon, we can beat anyone!")
}

text VerdanturfTown_WandasHouse_Text_StrongerSpeech_Pory {
  format("Wally: I lost to you, {PLAYER}, but I'm not feeling down anymore.\pBecause I have a new purpose in life. Together with my Pokémon, I'm going to challenge Pokémon Gyms and become a great Trainer.\pPlease watch me, {PLAYER}.\nI'm going to be stronger than you.\pWhen I do, I'm going to challenge you to another battle.")
}

text MatchCall_Text_Wally5_Pory {
  format("Wally: {PLAYER}? It's awesome!\nThat Pokémon we caught together? It evolved, {PLAYER}!\pMaybe I'm talented…\pOh, but I shouldn't get a big head like that!\pAfter all, it's the Pokémon that should be praised!")
}

// replaces mention of the Zigzagoon Wally borrows from Norman with a Chansey

text PetalburgCity_Gym_Text_IllLoanYouMyChansey_Pory {
  format("Wally, here, I'll loan you my Pokémon.\pWally received a Chansey!")
}

// moves Wally to the end of Victory Road like in Ruby and Sapphire

const LOCALID_WALLY = 6

script VictoryRoad_1F_OnTransition_Pory {
  if (var(VAR_VICTORY_ROAD_1F_STATE) == 1) {
    setobjectxyperm(LOCALID_WALLY, 31, 9)
    setobjectmovementtype(LOCALID_WALLY, MOVEMENT_TYPE_LOOK_AROUND)
  }
  end
}

script VictoryRoad_1F_EventScript_WallyTriggerLeft_Pory {
  lock
  addobject(LOCALID_WALLY)
  applymovement(LOCALID_WALLY, VictoryRoad_1F_Movement_WallyApproachPlayerLeft_Pory)
  waitmovement(0)
  faceplayer
  applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
  waitmovement(0)
  setvar(VAR_VICTORY_ROAD_1F_STATE, 1)
  goto(VictoryRoad_1F_EventScript_Wally_Pory)
  release
  end
}

script VictoryRoad_1F_EventScript_WallyTriggerCenter_Pory {
  lock
  addobject(LOCALID_WALLY)
  applymovement(LOCALID_WALLY, VictoryRoad_1F_Movement_WallyApproachPlayerCenter_Pory)
  waitmovement(0)
  faceplayer
  applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterDown)
  waitmovement(0)
  setvar(VAR_VICTORY_ROAD_1F_STATE, 1)
  goto(VictoryRoad_1F_EventScript_Wally_Pory)
  release
  end
}

script VictoryRoad_1F_EventScript_WallyTriggerRight_Pory {
  lock
  addobject(LOCALID_WALLY)
  applymovement(LOCALID_WALLY, VictoryRoad_1F_Movement_WallyApproachPlayerRight_Pory)
  waitmovement(0)
  faceplayer
  applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
  waitmovement(0)
  setvar(VAR_VICTORY_ROAD_1F_STATE, 1)
  goto(VictoryRoad_1F_EventScript_Wally_Pory)
  release
  end
}

movement VictoryRoad_1F_Movement_WallyApproachPlayerLeft_Pory {
  walk_up * 6
  face_left
}

movement VictoryRoad_1F_Movement_WallyApproachPlayerCenter_Pory {
  walk_up * 6
}

movement VictoryRoad_1F_Movement_WallyApproachPlayerRight_Pory {
  walk_up * 6
  face_right
}

script VictoryRoad_1F_EventScript_Wally_Pory {
  if (!flag(FLAG_DEFEATED_WALLY_VICTORY_ROAD)) {
  msgbox(VictoryRoad_1F_Text_WallyNotGoingToLoseAnymore)
  trainerbattle_no_intro(TRAINER_WALLY_VR_1, VictoryRoad_1F_Text_WallyEntranceDefeat)
  msgbox(VictoryRoad_1F_Text_WallyPostEntranceBattle)
  clearflag(FLAG_HIDE_VICTORY_ROAD_EXIT_WALLY)
  copyobjectxytoperm(LOCALID_WALLY)
  setflag(FLAG_DEFEATED_WALLY_VICTORY_ROAD)
  release
  end
  } elif (!flag(FLAG_IS_CHAMPION)) {
    lock
    faceplayer
    msgbox(VictoryRoad_1F_Text_WallyPostEntranceBattle)
    release
    end
  } else {
    goto(VictoryRoad_1F_EventScript_ExitWally)
  }
}
