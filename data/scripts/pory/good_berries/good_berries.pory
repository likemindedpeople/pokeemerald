script Route104_PrettyPetalFlowerShop_EventScript_RandomBerryGirl_Pory {
  lock
  faceplayer
  dotimebasedevents
  if (!flag(FLAG_DAILY_FLOWER_SHOP_RECEIVED_BERRY)) {
    msgbox(format("I'm trying to be like my big sisters. I'm growing flowers, too!\pHere you go! They're for you!"))
    if (!flag(FLAG_BADGE02_GET)) {
      call(Pory_EventScript_Berries_Status)
    } else {
      call(Pory_EventScript_Berries_Common)
    }
    setflag(FLAG_DAILY_FLOWER_SHOP_RECEIVED_BERRY)
  }
  msgbox(format("You can plant a Berry and grow it big, or you can make a Pokémon hold it.\pBut now they have a machine that mixes up different Berries and makes candies for Pokémon.\pI want some candy, too."))
  release
  end
}

script Route104_EventScript_ExpertF_Pory {
  lock
  faceplayer
  if (!flag(FLAG_RECEIVED_BERRY_ROUTE_104)) {
    msgbox(format("If you see Berries growing in loamy soil, feel free to take them.\pBut, make sure you plant a Berry in the same spot. That's common courtesy.\pHere, I'll share these with you."))
    call(Pory_EventScript_Berries_Retaliatory)
    setflag(FLAG_RECEIVED_BERRY_ROUTE_104)
  }
  msgbox(format("The way you look, you must be a Trainer, no?\pTrainers often make Pokémon hold Berries.\pIt's up to you whether to grow Berries or use them."))
  release
  end
}

script Route111_EventScript_Girl_Pory {
  lock
  faceplayer
  if (!flag(FLAG_DAILY_ROUTE_111_RECEIVED_BERRY)) {
    msgbox(format("I watered the plants every day. They grew lots of flowers.\pAnd they gave me lots of Berries, too.\pHere you go! You can have these!"))
    call(Pory_EventScript_Berries_Common)
    setflag(FLAG_DAILY_ROUTE_111_RECEIVED_BERRY)
    msgbox(format("I'm going to try really hard and make Berries in different colors.\pI hope you try hard, too!"))
  } else {
    msgbox(format("I wonder what color Berries I'll look for today?"))
  }
  release
  end
}

script Route114_EventScript_Man_Pory {
  lock
  faceplayer
  dotimebasedevents
  if (!flag(FLAG_DAILY_ROUTE_114_RECEIVED_BERRY)) {
    msgbox(format("I love using the Berry Crush machine, so I'm collecting Berries.\pI'll share some with you, if you'd like."))
    call(Pory_EventScript_Berries_Common)
    setflag(FLAG_DAILY_ROUTE_114_RECEIVED_BERRY)
    msgbox(format("You should try the Berry Crush machine with your friends."))
  } else {
    msgbox(format("Which Berry should you plant? Should you use or hoard Berries?\pIt's fun to think about."))
  }
  release
  end
}

script Route120_EventScript_BerryBeauty_Pory {
  lock
  faceplayer
  dotimebasedevents
  if (!flag(FLAG_DAILY_ROUTE_120_RECEIVED_BERRY)) {
    msgbox(format("Berries grow by soaking up sunlight.\nWe help the Berries grow by watering them regularly.\pIt's an expression of love, isn't it?\pYes, yes. You understand what I mean. You should take these."))
    call(Pory_EventScript_Berries_Common)
    setflag(FLAG_DAILY_ROUTE_120_RECEIVED_BERRY)
    msgbox(format("I think those Berries are rare. I hope you raise them with loving care."))
  } else {
    msgbox(format("I'm going to get more Berries from the Berry Master, I think."))
  }
  release
  end
}

script LilycoveCity_EventScript_BerryGentleman_Pory {
  lock
  faceplayer
  dotimebasedevents
  if (!flag(FLAG_DAILY_LILYCOVE_RECEIVED_BERRY)) {
    msgbox(format("When it gets right down to it…\pThe same way suits suit me perfectly, a crisp breeze suits the sea.\pAnd you, some Berries suit you to a “T”…\pWhy should that be so?"))
    call(Pory_EventScript_Berries_Common)
    setflag(FLAG_DAILY_LILYCOVE_RECEIVED_BERRY)
    msgbox(format("When it gets right down to it…\pIt's because you're a Trainer!"))
  } else {
    msgbox(format("When it gets right down to it…\pThe way dignified simplicity suits me, {POKEBLOCK}s perfectly suit Pokémon."))
  }
  release
  end
}

script Route123_BerryMastersHouse_EventScript_BerryMaster_Pory {
  lock
  faceplayer
  dotimebasedevents
  if (!flag(FLAG_DAILY_BERRY_MASTER_RECEIVED_BERRY)) {
    msgbox(format("You may call me the Berry Master.\pI dream of filling the world with beautiful flowers, so I raise Berries and hand them out to everyone.\pTake these!"))
    call(Pory_EventScript_Berries_Resistance)
    setflag(FLAG_DAILY_BERRY_MASTER_RECEIVED_BERRY)
    msgbox(format("Why be stingy?\nTake some more!"))
    call(Pory_EventScript_Berries_Resistance)
    msgbox(format("Be sure to visit the Pretty Petal flower shop near Rustboro.\pLet flowers fill the world!"))
  } else {
    msgbox(format("I'm done for today. Come again another day.\pLet flowers fill the world!"))
  }
  release
  end
}

// removed Easy Chat system and now goes straight to her GiveNormalBerry functionality
script Route123_BerryMastersHouse_EventScript_BerryMastersWife_Pory {
  lock
  faceplayer
  dotimebasedevents
  if (!flag(FLAG_DAILY_BERRY_MASTERS_WIFE)) {
    msgbox(format("The way my husband grows Berries, oh, he's the best in the world.\pHe makes me proud, that he does.\pAh…\nI want you to have these."))
    call(Pory_EventScript_Berries_RaiseStats)
    setflag(FLAG_DAILY_BERRY_MASTERS_WIFE)
  }
  msgbox(format("Our four grandchildren are becoming more accomplished than my husband.\pJoy never goes out of my life!"))
  release
  end
}

// now gives any of the Confuse berries, and doesn't give any EV/Friendship berries
script SootopolisCity_EventScript_KiriGiveBerry_Pory {
  dotimebasedevents
  special(GetPlayerBigGuyGirlString)
  if (!flag(FLAG_DAILY_SOOTOPOLIS_RECEIVED_BERRY)) {
    msgbox(format("Hi, what your name?\p… … …  … … …  … … …\nOkay!\pI'm Kiri. Mommy and Daddy named me so I grow big and strong. They wished!\pHere, you have it!"))
    call(Pory_EventScript_Berries_Common)
    setflag(FLAG_DAILY_SOOTOPOLIS_RECEIVED_BERRY)
    msgbox(format("Give you this, too! I like it lots!"))
    call(Pory_EventScript_Berries_Common)
    msgbox(format("What wish is your name?"))
  } else {
    msgbox(format("Spring, summer, fall, winter.\pBorn in spring, you like spring? You born in summer, you like summer?"), MSGBOX_YESNO)
    if (var(VAR_RESULT)) {
      msgbox(format("Kiri was born in fall, I love fall!\pYou like what one?"))
    } else {
      msgbox(format("Oh…\nno matter…\pSo much I want to know…"))
    }
  }
  release
  end
}

// Helper scripts

script Pory_EventScript_Berries_Common {
  random(10)
  switch (var(VAR_RESULT)) {
    case 0: giveitem(ITEM_LEPPA_BERRY,  3)
    case 1: giveitem(ITEM_LEPPA_BERRY,  3)
    case 2: giveitem(ITEM_LEPPA_BERRY,  3)
    case 3: giveitem(ITEM_LUM_BERRY,    3)
    case 4: giveitem(ITEM_CHILAN_BERRY, 3)
    case 5: call(Pory_EventScript_Berries_Status)
    case 6: call(Pory_EventScript_Berries_Status)
    case 7: call(Pory_EventScript_Berries_HP)
    case 8: call(Pory_EventScript_Berries_HP)
    case 9: call(Pory_EventScript_Berries_Confuse)
  }
  goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
}

script Pory_EventScript_Berries_Status {
  random(6)
  switch (var(VAR_RESULT)) {
    case 0: giveitem(ITEM_CHERI_BERRY,  3)
    case 1: giveitem(ITEM_CHESTO_BERRY, 3)
    case 2: giveitem(ITEM_PECHA_BERRY,  3)
    case 3: giveitem(ITEM_RAWST_BERRY,  3)
    case 4: giveitem(ITEM_ASPEAR_BERRY, 3)
    case 5: giveitem(ITEM_PERSIM_BERRY, 3)
  }
  goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
}

script Pory_EventScript_Berries_HP {
  random(4)
  switch (var(VAR_RESULT)) {
    case 0: giveitem(ITEM_ORAN_BERRY,   3)
    case 1: giveitem(ITEM_SITRUS_BERRY, 3)
    case 2: giveitem(ITEM_NANAB_BERRY,  3)
    case 3: giveitem(ITEM_PINAP_BERRY,  3)
  }
  goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
}

script Pory_EventScript_Berries_Confuse {
  random(5)
  switch (var(VAR_RESULT)) {
    case 0: giveitem(ITEM_FIGY_BERRY,   3)
    case 1: giveitem(ITEM_WIKI_BERRY,   3)
    case 2: giveitem(ITEM_MAGO_BERRY,   3)
    case 3: giveitem(ITEM_AGUAV_BERRY,  3)
    case 4: giveitem(ITEM_IAPAPA_BERRY, 3)
  }
  goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
}

script Pory_EventScript_Berries_Retaliatory {
  random(4)
  switch (var(VAR_RESULT)) {
    case 0: giveitem(ITEM_JABOCA_BERRY,  3)
    case 1: giveitem(ITEM_ROWAP_BERRY,   3)
    case 2: giveitem(ITEM_KEE_BERRY,     3)
    case 3: giveitem(ITEM_MARANGA_BERRY, 3)
  }
  goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
}

script Pory_EventScript_Berries_Resistance {
  random(17)
  switch (var(VAR_RESULT)) {
    case  0: giveitem(ITEM_OCCA_BERRY,   3)
    case  1: giveitem(ITEM_PASSHO_BERRY, 3)
    case  2: giveitem(ITEM_WACAN_BERRY,  3)
    case  3: giveitem(ITEM_RINDO_BERRY,  3)
    case  4: giveitem(ITEM_YACHE_BERRY,  3)
    case  5: giveitem(ITEM_CHOPLE_BERRY, 3)
    case  6: giveitem(ITEM_KEBIA_BERRY,  3)
    case  7: giveitem(ITEM_SHUCA_BERRY,  3)
    case  8: giveitem(ITEM_COBA_BERRY,   3)
    case  9: giveitem(ITEM_PAYAPA_BERRY, 3)
    case 10: giveitem(ITEM_TANGA_BERRY,  3)
    case 11: giveitem(ITEM_CHARTI_BERRY, 3)
    case 12: giveitem(ITEM_KASIB_BERRY,  3)
    case 13: giveitem(ITEM_HABAN_BERRY,  3)
    case 14: giveitem(ITEM_COLBUR_BERRY, 3)
    case 15: giveitem(ITEM_BABIRI_BERRY, 3)
    case 16: giveitem(ITEM_ROSELI_BERRY, 3)
  }
  goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
}

script Pory_EventScript_Berries_RaiseStats {
  specialvar(VAR_RESULT, GetPlayerTrainerIdOnesDigit)
  switch (var(VAR_RESULT)) {
    case 0:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_LIECHI_BERRY, 3)
        case 1: giveitem(ITEM_GANLON_BERRY, 3)
      }
    case 1:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_LIECHI_BERRY, 3)
        case 1: giveitem(ITEM_SALAC_BERRY,  3)
      }
    case 2:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_LIECHI_BERRY, 3)
        case 1: giveitem(ITEM_PETAYA_BERRY, 3)
      }
    case 3:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_LIECHI_BERRY, 3)
        case 1: giveitem(ITEM_APICOT_BERRY, 3)
      }
    case 4:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_GANLON_BERRY, 3)
        case 1: giveitem(ITEM_SALAC_BERRY,  3)
      }
    case 5:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_GANLON_BERRY, 3)
        case 1: giveitem(ITEM_PETAYA_BERRY, 3)
      }
    case 6:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_GANLON_BERRY, 3)
        case 1: giveitem(ITEM_APICOT_BERRY, 3)
      }
    case 7:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_SALAC_BERRY,  3)
        case 1: giveitem(ITEM_PETAYA_BERRY, 3)
      }
    case 8:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_SALAC_BERRY,  3)
        case 1: giveitem(ITEM_APICOT_BERRY, 3)
      }
    case 9:
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: giveitem(ITEM_PETAYA_BERRY, 3)
        case 1: giveitem(ITEM_APICOT_BERRY, 3)
      }
  }
  goto_if_eq(VAR_RESULT, FALSE, Common_EventScript_ShowBagIsFull)
}

// speeds up berry tree interactions

script BerryTree_EventScript_WantToPlant_Pory {
  lock
  msgbox(format("It's soft, loamy soil."))
  fadescreen(FADE_TO_BLACK)
  closemessage
  special(Bag_ChooseBerry)
  waitstate
  goto_if_eq(VAR_ITEM_ID, 0, BerryTree_EventScript_CancelPlanting)
  removeitem(VAR_ITEM_ID)
  call(BerryTree_EventScript_PlantBerry)
  goto(BerryTree_EventScript_WantToWater)
  release
  end
}

text BerryTree_Text_WantToPick_Pory {
  "Pick the {STR_VAR_2} {STR_VAR_1}?"
}


text BerryTree_Text_PlantedOneBerry_Pory {
  "Planted the {STR_VAR_1}."
}

text BerryTree_Text_BerryGrowthStage2_Pory {
  "The {STR_VAR_1} has sprouted!"
}

text BerryTree_Text_WantToWater_Pory {
  "Want to water it?"
}
