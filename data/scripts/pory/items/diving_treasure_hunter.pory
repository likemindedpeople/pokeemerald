// the Diving Treasure Hunter now gives HM08 Dive and is a Pearl maniac

script Pory_EventScript_DivingTreasureHunter {
  lock
  faceplayer
  if (!flag(FLAG_MET_DIVING_TREASURE_HUNTER)) {
    msgbox(format("I'm the Diving Treasure Hunter!\pI'm the awesome dude who makes deep-sea dives to gather all the Pearls resting at the bottom.\pTell me, have you ever gone diving with your Pokémon?\pNo? That's a downer. Well, I'll tell you what. Take this!"))
    giveitem(ITEM_HM_DIVE)
    setflag(FLAG_RECEIVED_HM_DIVE)
    setflag(FLAG_OMIT_DIVE_FROM_STEVEN_LETTER)
    msgbox(format("That's the Hidden Machine Dive. While you're using Surf, you may notice darker patches of water.\pUse Dive in those places, and you'll drop to the seafloor. When you want to come back up, use Dive again. You won't be able to surface in some places, though."))
    msgbox(format("Hope you bring me back some sweet Pearls you find down there diving with your Pokémon, friend!"))
    setflag(FLAG_MET_DIVING_TREASURE_HUNTER)
    delay(30)
  }
  checkitem(ITEM_BIG_PEARL)
  if (var(VAR_RESULT)) {
    checkitem(ITEM_PEARL)
    if (var(VAR_RESULT)) {
      random(2)
      switch (var(VAR_RESULT)) {
        case 0: call(Pory_EventScript_DivingTreasureHunter_DoBigPearls)
        case 1: call(Pory_EventScript_DivingTreasureHunter_DoPearls)
      }
    } else {
      call(Pory_EventScript_DivingTreasureHunter_DoBigPearls)
    }
  } else {
    checkitem(ITEM_PEARL)
    if (var(VAR_RESULT)) {
      call(Pory_EventScript_DivingTreasureHunter_DoPearls)
    } else {
      msgbox(format("Hope you bring me back some sweet Pearls you find down there diving with your Pokémon, friend!"))
    }
  }
  release
  end
}

script Pory_EventScript_DivingTreasureHunter_DoBigPearls {
  showmoneybox(0, 0)
  checkitem(ITEM_BIG_PEARL, 2)
  if (var(VAR_RESULT)) {
    msgbox(format("Hey, you wanna give me one of those Big Pearls you got? How about ¥8000 for one?"), MSGBOX_YESNO)
  } else {
    msgbox(format("Hey, you wanna give me that Big Pearl you got? How about ¥8000 for it?"), MSGBOX_YESNO)
  }
  if (var(VAR_RESULT)) {
    msgbox(format("{PLAYER} handed the Big Pearl to the dude."))
    removeitem(ITEM_BIG_PEARL)
    addmoney(8000)
    updatemoneybox
    playse(SE_SHOP)
    msgbox(format("Narly bro, thanks!"))
  } else {
    msgbox(format("Man, that really harshes my mellow! Too bad!"))
  }
  hidemoneybox
}

script Pory_EventScript_DivingTreasureHunter_DoPearls {
  showmoneybox(0, 0)
  checkitem(ITEM_PEARL, 2)
  if (var(VAR_RESULT)) {
    msgbox(format("Hey, you wanna give me one of those Pearls you got? How about ¥2000 for one?"), MSGBOX_YESNO)
  } else {
    msgbox(format("Hey, you wanna give me that Pearl you got? How about ¥2000 for it?"), MSGBOX_YESNO)
  }
  if (var(VAR_RESULT)) {
    msgbox(format("{PLAYER} handed the Pearl to the dude."))
    removeitem(ITEM_PEARL)
    addmoney(2000)
    updatemoneybox
    playse(SE_SHOP)
    msgbox(format("Narly bro, thanks!"))
  } else {
    msgbox(format("Totally uncool, dude! Maybe next time though!"))
  }
  hidemoneybox
}

script Route124_EventScript_HuntersHouseSign_Pory {
  msgbox(format("Diving Treasure Hunter's House\n“We buy Pearls!”"))
  release
  end
}

script Route124_DivingTreasureHuntersHouse_EventScript_ShardTradeBoard_Pory {
  msgbox(format("A sign that looks like it should say\n“We accept Visa” but actually used to be a menu of different items for trade."))
  release
  end
}
