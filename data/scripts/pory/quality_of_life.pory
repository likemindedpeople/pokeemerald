// makes the Move Tutor's new in-game name the "Move Reminder" and his services free

script FallarborTown_EventScript_MoveTutorSign_Pory {
  msgbox(format("Move Reminder's House\n“Moves taught to Pokémon.”"), MSGBOX_SIGN)
  release
  end
}

script FallarborTown_MoveRelearnersHouse_EventScript_MoveRelearner_Pory {
  lock
  faceplayer
  if (!flag(FLAG_TEMP_1)) {
    msgbox(format("I'm the Move Reminder.\pI know all the moves that Pokémon\nlearn--every one of them--and I can\lmake Pokémon remember those moves.\pI can do it with some of yours if you'd like."))
  }
  setflag(FLAG_TEMP_1)
  msgbox(format("You want me to remind one of them?"), MSGBOX_YESNO)
  if (var(VAR_RESULT)) {
    call(Pory_EventScript_MoveRelearner)
  }
  msgbox(format("If your Pokémon ever need to remember some moves, you know where to find me!"))
  release
  end
}

script Pory_EventScript_MoveRelearner {
  if (!flag(FLAG_TEMP_2)) {
    msgbox(format("Which one needs reminding?"))
  } else {
    msgbox(format("Any others?"))
  }
  setflag(FLAG_TEMP_2)
  special(ChooseMonForMoveRelearner)
  waitstate
  if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
    clearflag(FLAG_TEMP_2)
    msgbox(format("Come back if your Pokémon ever need help remembering moves!"))
    release
    end
  } else {
    special(IsSelectedMonEgg)
    if (var(VAR_RESULT)) {
      msgbox(format("Huh? There isn't a single move that\nI can teach an Egg."))
      call(Pory_EventScript_MoveRelearner)
    }
    if (var(VAR_0x8005) == 0) {
      msgbox(format("Sorry… It doesn't appear as if there's any move I can help that Pokémon remember."))
      call(Pory_EventScript_MoveRelearner)
    }
    msgbox(format("What move should I remind them of?"))
    special(TeachMoveRelearnerMove)
    waitstate
    call(Pory_EventScript_MoveRelearner)
  }
}

// makes the granny on Mt. Chimney now sell Lava Cookies by the dozen

script MtChimney_EventScript_LavaCookieLady_Pory {
  lock
  faceplayer
  showmoneybox(0, 0)
  msgbox(MtChimney_Text_LavaCookiesJust200_Pory, MSGBOX_YESNO)
  goto_if_eq(VAR_RESULT, NO, MtChimney_EventScript_DeclineLavaCookie)
  checkmoney(2400)
  goto_if_eq(VAR_RESULT, FALSE, MtChimney_EventScript_NotEnoughMoney)
  msgbox(MtChimney_Text_ThankYouDear, MSGBOX_DEFAULT)
  checkitemspace(ITEM_LAVA_COOKIE)
  call_if_eq(VAR_RESULT, TRUE, MtChimney_EventScript_RemoveMoney_Pory)
  giveitem(ITEM_LAVA_COOKIE, 12)
  goto_if_eq(VAR_RESULT, FALSE, MtChimney_EventScript_BagIsFull)
  hidemoneybox
  release
  end
}

script MtChimney_EventScript_RemoveMoney_Pory {
  removemoney(2400)
  updatemoneybox
  return
}

text MtChimney_Text_LavaCookiesJust200_Pory { // actually ¥2400 obviously
  format("Lava Cookies are Mt. Chimney's local specialty.\pTry some! They're just ¥2400 a dozen.")
}

// makes Nurse Joy go faster and talk less

script Pory_EventScript_Nurse {
  lock
  faceplayer
  setvar(VAR_0x8004, 0)
  specialvar(VAR_RESULT, CountPlayerTrainerStars)
  if (var(VAR_RESULT) == 4) {
    if (flag(FLAG_NURSE_MENTIONS_GOLD_CARD)) {
      msgbox(gText_YouWantTheUsual, MSGBOX_YESNO)
      if (var(VAR_RESULT) == YES) {
        setvar(VAR_0x8004, 1)
        goto(EventScript_PkmnCenterNurse_HealPkmn_Pory)
      } else {
        message(gText_WeHopeToSeeYouAgain2)
      }
    } else {
      setflag(FLAG_NURSE_MENTIONS_GOLD_CARD)
      msgbox(gText_WelcomeCutShort)
      playse(SE_PIN)
      applymovement(VAR_0x800B, Common_Movement_ExclamationMark)
      waitmovement(0)
      applymovement(VAR_0x800B, Common_Movement_Delay48)
      waitmovement(0)
      msgbox(gText_NoticesGoldCard, MSGBOX_YESNO)
      if (var(VAR_RESULT) == YES) {
        setvar(VAR_0x8004, 1)
        goto(EventScript_PkmnCenterNurse_HealPkmn_Pory)
      } else {
        message(gText_WeHopeToSeeYouAgain2)
      }
    }
  } else {
    call(Pory_EventScript_Nurse_Hellos)
    goto(EventScript_PkmnCenterNurse_HealPkmn_Pory)
  }
}

// VAR_0x8004 is 1 when player has Gold Card
script EventScript_PkmnCenterNurse_HealPkmn_Pory {
  incrementgamestat(GAME_STAT_USED_POKECENTER)
  if (var(VAR_0x8004) == 1) {
    message(gText_IllTakeYourPkmn2)
  } else {
    call(Pory_EventScript_Nurse_IllTakeYourPkmn)
  }
  waitmessage
  applymovement(VAR_0x800B, Common_Movement_WalkInPlaceFasterLeft)
  waitmovement(0)
  dofieldeffect(FLDEFF_POKECENTER_HEAL)
  waitfieldeffect(FLDEFF_POKECENTER_HEAL)
  applymovement(VAR_0x800B, Common_Movement_WalkInPlaceFasterDown)
  waitmovement(0)
  special(HealPlayerParty)
  if (!flag(FLAG_POKERUS_EXPLAINED)) {
    specialvar(VAR_RESULT, IsPokerusInParty)
    if (var(VAR_RESULT) == TRUE) {
      message(gText_PokerusExplanation)
      setflag(FLAG_POKERUS_EXPLAINED)
      return
    }
  }
  specialvar(VAR_RESULT, PlayerNotAtTrainerHillEntrance)
  if (var(VAR_RESULT) == 0) {
    goto(EventScript_PkmnCenterNurse_ReturnPkmn_Pory)
  }
  specialvar(VAR_RESULT, BufferUnionRoomPlayerName)
  copyvar(VAR_0x8008, VAR_RESULT)
  if (var(VAR_0x8008) == 0 || flag(FLAG_NURSE_UNION_ROOM_REMINDER)) {
    goto(EventScript_PkmnCenterNurse_ReturnPkmn_Pory)
  }
  msgbox(gText_RestoredPkmnToFullHealth)
  setflag(FLAG_NURSE_UNION_ROOM_REMINDER)
  message(CableClub_Text_PlayerIsWaiting)
  waitmessage
  applymovement(VAR_0x800B, Movement_PkmnCenterNurse_Bow)
  waitmovement(0)
  call(Pory_EventScript_Nurse_Goodbyes)
}

// VAR_0x8004 is 1 when player has Gold Card
script EventScript_PkmnCenterNurse_ReturnPkmn_Pory {
  if (var(VAR_0x8004) == 1) {
    message(gText_ThankYouForWaiting)
    waitmessage
    applymovement(VAR_0x800B, Movement_PkmnCenterNurse_Bow)
    waitmovement(0)
    message(gText_WeHopeToSeeYouAgain2)
  } else {
    call(Pory_EventScript_Nurse_RestoredPkmnToFullHealth)
    waitmessage
    applymovement(VAR_0x800B, Movement_PkmnCenterNurse_Bow)
    waitmovement(0)
    call(Pory_EventScript_Nurse_Goodbyes)
  }
}

script Common_EventScript_PkmnCenterNurse_Pory {
  call(Pory_EventScript_Nurse)
}

// Helper scripts

script Pory_EventScript_Nurse_Hellos {
  if (flag(FLAG_SYS_GAME_CLEAR)) {
    random(8)
    switch (var(VAR_RESULT)) {
    case 0: msgbox(format("{FONT_SMALL}…is …is that the league champion?!"))
    case 1: msgbox(format("{FONT_SMALL}…I'm pretty sure that's the champion!"))
    }
  }
  if (!flag(FLAG_BADGE01_GET)) {
    random(3)
    switch (var(VAR_RESULT)) {
      case 0: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case 1: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case 2: msgbox(format("Hi! Welcome to our center!"))
    }
  } else {
    random(13)
    switch (var(VAR_RESULT)) {
      case 0: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case 1: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case 2: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case 3: msgbox(format("Hello, and welcome to the Pokémon Center."))
      case 4: msgbox(format("Hi! Welcome to our center!"))
      case 5: msgbox(format("Hi! Welcome to our center!"))
      case 6: msgbox(format("Hi! Welcome to our center!"))
      case 7: msgbox(format("Hello, hello! Back again?"))
      case 8: msgbox(format("Hello, hello! Back again?"))
      case 9: msgbox(format("Hey! How's it going?"))
      case 10: msgbox(format("Hey! How's it going?"))
      case 11: msgbox(format("{FONT_SMALL}…sheesh, again already? seriously?… {FONT_NORMAL}Hello! Welcome!"))
      case 12: msgbox(format("Hey… You do realize that this is free healthcare, right?"))
    }
  }
}

script Pory_EventScript_Nurse_IllTakeYourPkmn {
  random(3)
  switch (var(VAR_RESULT)) {
    case 0: message(format("I'll take your Pokémon for a few seconds…"))
    case 1: message(format("Let me see your Pokémon for just a moment…"))
    case 2: message(format("I'll have these back to you in no time…"))
  }
}

script Pory_EventScript_Nurse_RestoredPkmnToFullHealth {
  random(4)
  switch (var(VAR_RESULT)) {
    case 0: message(format("Thank you for waiting."))
    case 1: message(format("We've restored your Pokémon to full health."))
    case 2: message(format("Thanks for waiting!"))
    case 3: message(format("Your Pokémon look as happy and healthy as ever!"))
  }
}

script Pory_EventScript_Nurse_Goodbyes {
  if (!flag(FLAG_BADGE01_GET)) {
    random(3)
    switch (var(VAR_RESULT)) {
      case 0: message(format("Until next time!"))
      case 1: message(format("See you later!"))
      case 2: message(format("Goodbye!"))
    }
  } else {
    random(9)
    switch (var(VAR_RESULT)) {
      case 0: message(format("Until next time!"))
      case 1: message(format("See you later!"))
      case 2: message(format("Goodbye!"))
      case 3: message(format("Bye bye!"))
      case 4: message(format("Bye now!"))
      case 5: message(format("Bye!"))
      case 6: message(format("Ciao!"))
      case 7: message(format("Good luck out there!"))
      case 8: message(format("Have fun!"))
    }
  }
}
